---
output: github_document
---

```{r setup, include = F}
# devtools::install_github("Hemken/Statamarkdown")
library(Statamarkdown)
```

# Hansen DWI Replication

**Directions:** Download `hansen_dwi.dta` from GitHub at the following address. Note these data are not exactly the same as his because of confidentiality issues (so he couldn’t share all of it).

https://github.com/scunning1975/causal-inference-class/raw/master/hansen_dwi

```{stata load-data, collectcode = TRUE}
********************************************************************************
* name: hansen.do
* author: scott cunningham (baylor)
* description: replicate figures and tables in Hansen 2015 AER
* last updated: december 5, 2021
********************************************************************************

* Install packages:
* ssc install estout
* net install rdrobust, from(https://raw.githubusercontent.com/rdpackages/rdrobust/master/stata) replace
* ssc install rdrobust, replace
* net install rddensity, from(https://raw.githubusercontent.com/rdpackages/rddensity/master/stata) replace
* net install lpdensity, from(https://sites.google.com/site/nppackages/lpdensity/stata) replace

* load the data from github
use "https://github.com/scunning1975/causal-inference-class/raw/master/hansen_dwi.dta", clear
```

The outcome variable is `recidivism` which is measuring whether the person showed back up in the data within 4 months. Use this data to answer the following questions.

1. We will only focus on the 0.08 BAC cutoff; not the 0.15 cutoff. Take the following steps.
  a. Create a treatment variable (`dui`) equaling 1 if `bac1 >= 0.08` and 0 otherwise in your do/R file.

```{stata create-dui, collectcode = TRUE}
*-> 1.a. create dui treatment variable for bac1>=0.08
gen dui = 0
replace dui = 1 if bac1 >= 0.08 & bac1 ~= . 
// Stata when it sees a period (missing) in a variable it thinks that the
// observation is equal to positive infinity. And so since positive infinity
// is greater than 0.08, it will assign dui = 1 for that missing value which 
// can create major problems. 
```
  
  b. Store `bac1` into `bac1_orig` and then center the `bac1` variable, i.e. subtract $0.08$ so that the cutoff is now zero. 

```{stata recenter, collectcode = TRUE}
*-> 1.b. Re-center our running variable at bac1=0.08
rename bac1 bac1_orig
gen bac1 = bac1_orig - 0.08
```

  c. Replicate Hansen’s Figure 1 examining whether there is any evidence for manipulation on the running variable. Produce a raw histogram using `bac1`, then use the density test in Cattaneo, Titunik and Farrell’s `rddensity` package. Can you find any evidence for manipulation? What about heaping?

```{stata manipulation-heaping-hist, echo = 1:2}
*-> 1.c. Find evidence for manipulation or heaping using histograms
histogram bac1, discrete width(0.001) ytitle(Density) xtitle(Running variable (blood alcohol content)) xline(0.0) title(Density of observations across the running variable)
quietly graph export "hist.svg", replace
```

![Histogram of centered `bac1`](hist.svg)

```{stata, rddensity, echo = 1:2}
* use the Cattaneo, et al. -rddensity-
rddensity bac1, c(0.0) plot
quietly graph export "rddensity.svg", replace
```

![Results of rddensity command](rddensity.svg)


2. We are going to test for manipulation around the cutoff (following Table 2 Panel A). Run RD regressions using a local-linear estimator on `white`, `male`, age (`aged`) and accident (`acc`) as dependent variables. Are the covariates balanced at the cutoff? Use data in `bac1_orig` 0.03 to 0.13 (or `bac1` in -0.05 to 0.05). Check if the results are robust to a more narrow bandwidth of `bac1_orig` in 0.055 to 0.105.

```{stata covariates-balance}
*-> 2. Are the covariates balanced at the cutoff? 
* Use two separate bandwidths (0.03 to 0.13; 0.055 to 0.105)
* yi = Xi′γ + α1 DUIi + α2 BACi + α3 BACi × DUIi + ui
eststo clear
foreach y of varlist white male acc aged {
  eststo: quietly reg `y' i.dui##c.bac1 if bac1_orig >= 0.03 & bac1_orig <= 0.13, robust
}
esttab, keep(1.dui)

eststo clear
foreach y of varlist white male acc aged {
  eststo: quietly reg `y' dui##c.bac1 if bac1_orig >= 0.055 & bac1_orig <= 0.105, robust
}
esttab, keep(1.dui)
```

3. Now, we turn our main result, estimating the effect of getting a DUI on recidivism (`recid`).
  a. Run an RD estimate using the `rdrobust` command (from the `rdrobust` package in R)

```{stata rdrobust}
*-> 3. Estimate RD of DUI on Recidivism
* Estimator first
rdrobust recid bac1, c(0) covs(white male aged acc)
```

  b. Like all RD applications, you need to include a plot of the underlying data. Plot the RD estimator using the `rdplot` command (from the `rdrobust` package in R).

```{stata rdplot, echo = 1:2}
* plot the data
rdplot recid bac1, c(0)
quietly graph export "rdplot.svg", replace
```

![RD Plot showing raw data](rdplot.svg)

4. Repeat but drop units in the close vicinity of 0.08 (0.079-0.081) (i.e., the "donut hole" regression). Do the results stay the same?

```{stata donut}
*-> 4. "donut hole" dropping close to 0.08
preserve
drop if bac1_orig>=0.079 & bac1_orig<=0.081
rdrobust recid bac1, c(0)
restore
```
